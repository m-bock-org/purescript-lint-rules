# Deliberately NOT putting node_modules/.bin on PATH. package.json still
# carries purs/spago/purs-tidy because CI installs them with npm, but a
# recipe run inside `nix develop` must use the compiler the flake pins -
# node_modules/.bin first on PATH silently wins over it.
set shell := ["bash", "-c"]

# Restores output/ first if there is none - a fresh clone then compiles
# nothing, because al-dente already built every dependency. Only when it
# is missing: once you have edited anything, the store copy is behind
# your working tree and replacing output/ would throw away exactly the
# incremental state that makes a rebuild fast.
build:
    #!/usr/bin/env bash
    set -euo pipefail
    [ -d output ] || just output
    spago build

# The gate's build: warnings are errors. purs does not re-report a
# warning for a module it did not recompile, so an incremental strict
# build can print zero while warnings genuinely exist. Dropping our own
# modules' output makes the count real without rebuilding every
# dependency, whose warnings are not ours to fix anyway.
strict:
    #!/usr/bin/env bash
    set -euo pipefail
    grep -rhoE '^module [A-Za-z0-9_.]+' src test 2>/dev/null \
      | awk '{print $2}' \
      | while read -r m; do rm -rf "output/$m"; done
    spago build --strict

format:
    purs-tidy format-in-place 'src/**/*.purs'

docs:
    spago test -m Test.Docs

# fails if the README's rules table is out of date with the rules
docs-check:
    #!/usr/bin/env bash
    set -euo pipefail
    before=$(mktemp)
    cp README.md "$before"
    spago test -m Test.Docs >/dev/null
    diff -u "$before" README.md

# The style, run as a binary rather than as a dependency.
#
# A dependency is not available to every repository - the regulator
# depends on several of them, so the arrow only goes one way - and the
# binary needs none: it reads the workspace it is run in, and
# `lint-exemptions.json` beside this file is where a departure goes.
#
# `--fix <command>` names a program that proposes fixes for findings the
# style has guidance for. The linter judges what comes back.
lint *ARGS:
    nix run git+ssh://git@github.com/m-bock-org/purescript-lint-regulator#lint-public -- {{ARGS}}

check: strict lint docs-check

# Restore output/ from the Nix build rather than compiling it here. A
# copy, not symlinks: purs writes into output/<Module>/ in place, and a
# read-only store symlink dies on the first local edit.
output:
    #!/usr/bin/env bash
    set -euo pipefail
    built="$(nix build .#testOutput --no-link --print-out-paths)"
    rm -rf output
    cp -aL "$built" output
    chmod -R u+w output
    echo "output/ restored from $built"

